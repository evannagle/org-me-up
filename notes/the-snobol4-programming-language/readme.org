#+TITLE: The Snobol4 Programming Language
#+AUTHOR: R. E. Griswald, J. F. Poage, I. P. Polonsky
#+DATE: 1971
#+EMAIL: evan@mantle.co
#+CREATOR: Evan Nagle
#+TAGS: @snobol @esoteric

* Chapter 1: Introduction
  :PROPERTIES:
  :header-args: :in-file data/todo :results scalar
  :END:
** Hello, World
*** Inline

#+BEGIN_SRC sh
snobol4 -b <<<'
# indentation matters:
  OUTPUT = "Hello, World"
END'
#+END_SRC

#+RESULTS:
: Hello, World

*** From a file
#+BEGIN_SRC sh
snobol4 -b data/hello.sno
#+END_SRC

#+RESULTS:
: Hello, World

*** From a file with input sources

#+BEGIN_SRC sh
cat data/jabberwocky.txt | snobol4 data/lines.sno 
#+END_SRC

#+RESULTS:
#+begin_example
1.          ’Twas brillig, and the slithy toves 
2.                Did gyre and gimble in the wabe: 
3.          All mimsy were the borogoves, 
4.                And the mome raths outgrabe. 
5.          
6.          “Beware the Jabberwock, my son! 
7.                The jaws that bite, the claws that catch! 
8.          Beware the Jubjub bird, and shun 
9.                The frumious Bandersnatch!” 
10.         
11.         He took his vorpal sword in hand; 
12.               Long time the manxome foe he sought— 
13.         So rested he by the Tumtum tree 
14.               And stood awhile in thought. 
15.         
16.         And, as in uffish thought he stood, 
17.               The Jabberwock, with eyes of flame, 
18.         Came whiffling through the tulgey wood, 
19.               And burbled as it came! 
20.         
21.         One, two! One, two! And through and through 
22.               The vorpal blade went snicker-snack! 
23.         He left it dead, and with its head 
24.               He went galumphing back. 
25.         
26.         “And hast thou slain the Jabberwock? 
27.               Come to my arms, my beamish boy! 
28.         O frabjous day! Callooh! Callay!” 
29.               He chortled in his joy. 
30.         
31.         ’Twas brillig, and the slithy toves 
32.               Did gyre and gimble in the wabe: 
33.         All mimsy were the borogoves, 
34.               And the mome raths outgrabe.
#+end_example
    
** Assignment: <space><variable> = <value>

The space is important. Without it, you'll get an error like this:

```
        NUM1VAR = "foo"
                 ^
-:2: *** Erroneous subject

ERRORS DETECTED IN SOURCE PROGRAM

```

#+BEGIN_SRC sh
snobol4 -b <<< '
 NUM1VAR = "foo"
 DOTTY..VAR = NUM1VAR
 DUNDER__VAR = DOTTY..VAR
 VAR = DUNDER__VAR
 OUTPUT = VAR
END'
#+END_SRC

#+RESULTS:
: foo

** Arithmetic
*** +, - * (), the usual candidates

#+BEGIN_SRC sh
snobol4 -b <<< '
  M = 4
  N = 8.2
  P = N * M / (N - 1)
  OUTPUT = P
END'
#+END_SRC

#+RESULTS:
: 4.55555555555556

*** ** = exponentiation

#+BEGIN_SRC sh
snobol4 -b <<< '
 OUTPUT = 3 ** 3
END'
#+END_SRC

#+RESULTS:
: 27
** Comments start with * or #

#+BEGIN_SRC sh
snobol4 -b <<<'
# Asterisks are more common, but they mess up org mode!
# I am just going to leave this here.
 OUTPUT = "Hello"
# And this here
 OUTPUT = "World"
END'
#+END_SRC

#+RESULTS:
: Hello
: World

** Lines can be extends with a +

#+BEGIN_SRC sh
snobol4 -b <<<'
      OUTPUT = "This is a really long piece of text"
+              " that spans two lines."
+              " Or make that three!"
END'
#+END_SRC

#+RESULTS:
: This is a really long piece of text that spans two lines. Or make that three!

** Strings
*** Can be specified via single or double quote.

#+BEGIN_SRC sh
snobol4 -b <<< '* This kind of escaping is only necessary when you are working inline
 APOST = "'"'"'" 
 QUOTE = '"'"'"'"'"'
 OUTPUT = QUOTE "You can" APOST "t always get what you want." QUOTE
END'
#+END_SRC

#+RESULTS:
: "You can't always get what you want."

*** Concatenated implicitly with an operator like awk: <s3> = <s1> <s2>

#+BEGIN_SRC sh
snobol4 -b <<< '
  A = "a"
  B = "b"
  C = "(" A B ")c"
  OUTPUT = C
END'
#+END_SRC

#+RESULTS:
: (ab)c

*** <variable> = <EOL> sets a string variable to NULL
*** Strings are implicitly converted in arithmetic expressions

#+BEGIN_SRC sh
snobol4 -b <<< '
 S4 = "4.0"
 N5 = S4 + 1.1
 OUTPUT = N5
 S5 = "-> " N5
 OUTPUT = S5
END'
#+END_SRC

#+RESULTS:
: 5.1
: -> 5.1
** Pattern matching
*** Replace a substring

#+BEGIN_SRC sh
snobol4 -b <<< '
 WORD = "Hello, World"
 WORD "e" = "_"
 OUTPUT = WORD
END'
#+END_SRC

#+RESULTS:
: H_llo, World

*** <variable><pattern> . <match> Replace a string and capture what you replaced

#+BEGIN_SRC sh
snobol4 -b <<<'
 PHRASE1 = "Hello, World"
 PHRASE2 = "Goodbye, World"
 PATTERN = ("H" | "G")
 PHRASE1 PATTERN . L = "_"
 OUTPUT = PHRASE1 " -> Replaced " L " with _"
 PHRASE2 PATTERN . L = "_"
 OUTPUT = PHRASE2 " -> Replaced " L " with _"
END'
#+END_SRC
#+RESULTS:
: _ello, World -> Replaced H with _
: _oodbye, World -> Replaced G with _

*** Replace a substring with string concatenation

The AHA here is consecutive patterns are matched consecutively. Here's an example:

#+BEGIN_SRC sh
snobol4 -b <<< '
# Unparenthesized consecutive patterns operate on the sequence of matches
# Parenthesized patterns operate on the previous match
# The . notation spits the matching pattern out to a variable
# The . notation can be inserted anywhere in the stream of patterns
 ALPHABET = "ABCDEFGHIJKLMNOPQRSUVWXYZ"
 ALPHABET "A" . OUTPUT
 ALPHABET ("A" "B") . OUTPUT
 ALPHABET ("AB") . OUTPUT
 ALPHABET (("A" | "B") ("A" | "B")) . OUTPUT
 ALPHABET ("A" | "B") ("B" | "C") . OUTPUT
 ALPHABET ALPHABET . OUTPUT
 ALPHABET "DEFG" LEN(4) ("L" | "M") . OUTPUT
END'
#+END_SRC

#+RESULTS:
: A
: AB
: AB
: AB
: B
: ABCDEFGHIJKLMNOPQRSUVWXYZ
: L

And from the book:

#+BEGIN_SRC sh
snobol4 -b <<< '
 HAND = "AC4DAHKDKS"
 RANK = 4
 SUIT = "D"
 HAND (RANK SUIT) = "--"
 OUTPUT = HAND
END'
#+END_SRC

#+RESULTS:
: AC--AHKDKS

*** Replace a substring using an OR pattern

#+BEGIN_SRC sh
snobol4 -b <<< '
 VOWELS = "a" | "e" | "i" | "o" | "u"
 HELLO = "Hello, World"
 HELLO VOWELS = "_"
 OUTPUT = HELLO
END'
#+END_SRC

#+RESULTS:
: H_llo, World

*** Conditional value assignment

#+BEGIN_SRC sh
snobol4 -b <<<'
 KEYWORD = "COMPUTER" | "PROGRAM"
 KEYWORD = KEYWORD | "ALGORITHM"
 TEXT = "PROGRAMMING ALGORITHMS FOR COMPUTERS"
 TEXT KEYWORD = "XXX"
 OUTPUT = TEXT
END'
#+END_SRC

#+RESULTS:
: XXXMING ALGORITHMS FOR COMPUTERS

*** Condition value assignment

#+BEGIN_SRC sh
snobol4 -b <<<'
 BASE = ("HEX" | "DEC") . B1
 NUM = "HEX4"
 NUM BASE = "_"
 OUTPUT = NUM
 OUTPUT = B1
END'
#+END_SRC

#+RESULTS:
: _4
: HEX

** Labels
*** Labels - like variables, but no space prefix.

#+BEGIN_SRC sh
snobol4 -b <<<'
L1 OUTPUT = "FOO" :S(L3)
 OUTPUT = "Skip Me!"
L3 OUTPUT = "DONE"
END'
#+END_SRC

#+RESULTS:
: FOO
: DONE

*** :S(<label>) = goto on success.
*** :F(<label>) = goto on fail or none

#+BEGIN_SRC sh
snobol4 -b <<<'
 LE(1,0) :F(LEAP)
 OUTPUT = "A"
LEAP OUTPUT = "B"
END
'
#+END_SRC

#+RESULTS:
: B

*** :(<label>) = goto no matter what, used as part of function definitions
*** :S(<label1>)F(<label2>) = goto label1 on success, label2 on error

#+BEGIN_SRC sh
snobol4 -b <<<'
L1 OUTPUT = "Line 1" :S(L3)F(L2)
L2 OUTPUT = "Something bad happened."
L3 OUTPUT = "Success."
END'
#+END_SRC

#+RESULTS:
: Line 1
: Success.

*** Aha! END is a label.

#+BEGIN_SRC sh
snobol4 -b <<< '
 OUTPUT = "Line 1" :S(END)
 OUTPUT = "Something bad happened."
END'
#+END_SRC

#+RESULTS:
: Line 1

** References
*** Indirect reference (pointers) with $

#+BEGIN_SRC sh
snobol4 -b <<<'
   MONTH = "APRIL"
#  APRIL = "CRUEL"
   $MONTH = "CRUEL"
   OUTPUT = APRIL
END'
#+END_SRC

#+RESULTS:
: CRUEL

*** Indirect reference is similar to Lisp's make-symbol

#+BEGIN_SRC sh
snobol4 -b <<<'
 MONTH1 = "APRIL"
 MONTH2 = "MAY"
 INDEX = 2
 OUTPUT = $("MONTH" INDEX)
END'
#+END_SRC

#+RESULTS:
: MAY

** Primitive functions
*** SIZE = string length

#+BEGIN_SRC sh
snobol4 -b <<<'
 APE = "SIMIAN"
 OUTPUT = SIZE(APE)
END'
#+END_SRC

#+RESULTS:
: 6

*** DUPL = repeat a string

#+BEGIN_SRC sh
snobol4 -b <<<'
 OUTPUT = DUPL("/*-.", 15)
 APE1 = "HOWARD"
 APE2 = "KIM"
 OUTPUT = DUPL(" ", 40 - SIZE(APE1)) APE1
 OUTPUT = DUPL(" ", 40 - SIZE(APE2)) APE2
END'
#+END_SRC

#+RESULTS:
: /*-./*-./*-./*-./*-./*-./*-./*-./*-./*-./*-./*-./*-./*-./*-.
:                                   HOWARD
:                                      KIM

*** REPLACE = replaces a *characters* in a string

#+BEGIN_SRC sh
snobol4 -b <<<'
 STATEMENT = "A(I,J) = A(I,J) + 3"
 OUTPUT = REPLACE(STATEMENT, "()", "<>")
END'
#+END_SRC

#+RESULTS:
: A<I,J> = A<I,J> + 3

*** LE = Less than or equal, LT = less than

#+BEGIN_SRC sh
snobol4 -b <<<'
     SUM = 0
     N = 0
ADD  N = LT(N,50) N + 1  :F(DONE)
     SUM = SUM + N     :(ADD)
DONE OUTPUT = SUM
END'
#+END_SRC

#+RESULTS:
: 1275

*** GE = Greater than or equal, GT = greater than
*** DIFFER = returns null if two arguments are not identical
*** LGT(A,B) = returns true if A is alphabetically > B
*** LEN(N) = pattern to grab N letters

#+BEGIN_SRC sh
snobol4 -b <<<'
       NAME = "Leroy"
       NAME  ("L" | "J") . LETTER1 ("e" | "a") . LETTER2 LEN(3) . THREE.MORE :S(FOUND)F(FAILED)
FOUND  OUTPUT = "Match!"
       OUTPUT = "First letter: " LETTER1
       OUTPUT = "Second letter: " LETTER2
       OUTPUT = "Next 3 letters: " THREE.MORE :(END)
FAILED OUTPUT = "Name did not match patterns."
END
'
#+END_SRC

#+RESULTS:

** Defined functions
*** DELETE
#+BEGIN_SRC sh
snobol4 -b <<<'
# The book example does not work...

        DEFINE("DELETE(STR,C)") :(SKP)
DELETE  STR C =                 :S(DELETE)
        DELETE = STR            :(RETURN)
SKP
        MAGIC = "ABACADABRA"
        OUTPUT = DELETE(MAGIC, "A")
END'
#+END_SRC

#+RESULTS:
: BCDBR
    
*** COMB

#+BEGIN_SRC sh
snobol4 -b <<<'
      DEFINE("COMB(STR,N,HEAD)CH")        :(SKP)
COMB  OUTPUT = "COMB(" STR ", " N ", " HEAD ") " CH
      OUTPUT = EQ(N,0) HEAD               :S(RETURN)
C2    STR LE(N, SIZE(STR)) LEN(1) . CH =  :F(RETURN)
      COMB(STR, N - 1, HEAD CH)           :(C2)
SKP

      COMB("ABCD", 2)
END'
#+END_SRC

#+RESULTS:
#+begin_example
COMB(ABCD, 2, ) 
COMB(BCD, 1, A) 
COMB(CD, 0, AB) 
AB
COMB(D, 0, AC) 
AC
COMB(, 0, AD) 
AD
COMB(CD, 1, B) 
COMB(D, 0, BC) 
BC
COMB(, 0, BD) 
BD
COMB(D, 1, C) 
COMB(, 0, CD) 
CD
#+end_example

** Keywords
*** &DUMP = flag to dump all natural variables at the end of execution

#+BEGIN_SRC sh
snobol4 -b <<<'
      &DUMP = 1
      FOO = "BAR"
END'
#+END_SRC

#+RESULTS:
#+begin_example
Dump of variables at termination

Natural variables

ABORT = PATTERN
ARB = PATTERN
BAL = PATTERN
FAIL = PATTERN
FENCE = PATTERN
FOO = 'BAR'
REM = PATTERN
SUCCEED = PATTERN

Unprotected keywords

&ABEND = 0
&ANCHOR = 0
&CASE = 1
&CODE = 0
&DUMP = 1
&ERRLIMIT = 0
&FTRACE = 0
&FULLSCAN = 0
&GTRACE = 0
&INPUT = 1
&MAXLNGTH = 4294967295
&OUTPUT = 1
&STLIMIT = -1
&TRACE = 0
&TRIM = 0
#+end_example

*** &TRIM = flag to trim 80 chars fed in through input
** Arrays
*** Array(<size>, <start value>)

#+BEGIN_SRC sh
snobol4 -b <<<'
      V = ARRAY(10, "1.0")
      V<6> = "2.0"
      OUTPUT = V<1>
      OUTPUT = V<6>
      OUTPUT = V<8>
# this fails silently, catch with :F(...)
      OUTPUT = V<11>
END'
#+END_SRC

#+RESULTS:
: 1.0
: 2.0
: 1.0

*** Array('<size>,<size>', <start value>) = multidimensional

#+BEGIN_SRC sh
snobol4 -b <<<'
                  DEFINE("dump.arr(arr,i,j)", "STR__dump.arr") :(END__dump.arr)
STR__dump.arr     
ITERI             i = i + 1
                  arr<i,1> :F(RETURN)
                  j = 0
ITERJ             j = j + 1
                  OUTPUT = arr<i,j> :S(ITERJ)F(ITERI)
END__dump.arr
                  V = ARRAY("2,2")
                  V<1,1> = "A"
                  V<1,2> = "B"
                  V<2,1> = "C"
                  V<2,2> = "D"

SHOW_V            dump.arr(V)      :S(SUCCESS)
                  OUTPUT = "Could not dump V, for one reason or another..."                  
SUCCESS
END'
#+END_SRC

#+RESULTS:
: A
: B
: C
: D

*** Hash tables

#+BEGIN_SRC sh
snobol4 -b <<<'
      CONTACTS = TABLE()
      CONTACTS<"Evan"> = "555-555-5551"
      CONTACTS<"Jaimie"> = "555-555-5552"
      OUTPUT = CONTACTS<"Evan"> ", " CONTACTS<"Jaimie">
END'
#+END_SRC

#+RESULTS:
: 555-555-5551, 555-555-5552

*** Custom data types

#+BEGIN_SRC sh
snobol4 -b <<<'
      DATA("PERSON(NAME,AGE,GENDER)")
      EVAN = PERSON("Evan Nagle", 35, "M")
      OUTPUT = NAME(EVAN) ", " AGE(EVAN) ", " GENDER(EVAN)
END
'
#+END_SRC

#+RESULTS:
: Evan Nagle, 35, M

** Program example: count letters

#+BEGIN_SRC sh
cat data/jabberwocky.txt | snobol4 data/letter-count.sno
#+END_SRC

#+RESULTS:
#+begin_example
Rows: 34
A: 61
B: 31
C: 16
D: 33
E: 80
F: 10
G: 21
H: 61
I: 37
J: 8
K: 7
L: 30
M: 26
N: 36
O: 53
P: 3
R: 33
S: 38
T: 67
U: 21
V: 6
W: 23
X: 1
Y: 16
#+end_example

* Chapter 2: Pattern Matching
  :PROPERTIES:
  :header-args: :results scalar
  :END:

** &ANCHOR = match only start of string

#+BEGIN_SRC sh
snobol4 -b <<<'
  OUTPUT = "-- PASS 1 --"
  &ANCHOR = 1
  "A BIG BOY" "BIG" . OUTPUT

  OUTPUT = "-- PASS 2 --"
  &ANCHOR = 0
  "A BIG BOY" "BIG" . OUTPUT
END
'
#+END_SRC

#+RESULTS:
| --  | PASS | 1 | -- |
| --  | PASS | 2 | -- |
| BIG |      |   |    |

** $ = immediate value assignment

#+BEGIN_SRC sh
snobol4 -b <<<'
# . assignment only succeeds if full pattern matches
# $ assignment succeeds if preceeding subpattern matches

  BR = (("B" | "R")  $ FIRST . FIRSTSUC ("E" | "EA") $ SECOND
+       ("D" | "DS") $ THIRD) . BRVAL

 OUTPUT = "BEATS"
 "BEATS" BR
 OUTPUT = FIRST
 OUTPUT = FIRSTSUC
 OUTPUT = SECOND
 OUTPUT = THIRD
 OUTPUT = BRVAL

 OUTPUT = "BEADS"
 "BEADS" BR
 OUTPUT = FIRST
 OUTPUT = FIRSTSUC
 OUTPUT = SECOND
 OUTPUT = THIRD
 OUTPUT = BRVAL
END
'
#+END_SRC

#+RESULTS:
| BEATS |
| B     |
|       |
| EA    |
|       |
|       |
| BEADS |
| B     |
| B     |
| EA    |
| D     |
| BEAD  |

** Value assignment is contingent on matches

#+BEGIN_SRC sh
snobol4 -b <<<'
 BR = ("B" | "R") . B_OR_R ("E" | "EA") . E_OR_EA ("D" | "DS") . D_OR_DS
 STR = "BEADS" 
 STR BR = B_OR_R "I" D_OR_DS
 OUTPUT = STR 
END
'
#+END_SRC

#+RESULTS:
: BIDS

** @ = cursor position at point of match

#+BEGIN_SRC sh :results scalar value
snobol4 -b <<<'
      &TRIM = 1
      P = ("B" | "R") ("E" | "EA") ("D" | "DS")
      PATTERN = @X P @Y
      STR = "THE BEADS ARE RED"
      STR PATTERN
      OUTPUT = STR
      OUTPUT = DUPL(" ", X) DUPL("^", Y - X + 1)
END
'
#+END_SRC

#+RESULTS:
: THE BEADS ARE RED
:     ^^^^^

** LEN, SPAN, BREAK revisited

#+BEGIN_SRC sh
snobol4 -b <<<'
  STR = "HERE IS A STRING WITH  SPACES"
  STR SPAN(" ") SPAN("ABCDEFGHIJKLMNOPQRSUVWXYZ") . OUTPUT
  STR BREAK(" ") . OUTPUT
  STR LEN(3) . OUTPUT
END
'
#+END_SRC

#+RESULTS:
| IS   |
| HERE |
| HER  |

** ANY, NOTANY = match single character

#+BEGIN_SRC sh
snobol4 -b <<<'
  STR = "BEARCLAW"
  STR ANY("AEIOU") . OUTPUT
  STR NOTANY("ABCDE") . OUTPUT
END'
#+END_SRC

#+RESULTS:
| E |
| R |

** TAB(N) = all characters from current position through Nth character

#+BEGIN_SRC sh
snobol4 -b <<<'
 STR = "SNOBOL4"
 STR LEN(2) TAB(5) . OUTPUT
END
'
#+END_SRC

#+RESULTS:
: OBO

** RTAB(N) = all but N characters matched after current position

#+BEGIN_SRC sh
snobol4 -b <<<'
 STR = "SNOBOL4"
 STR LEN(2) RTAB(1) . OUTPUT
END'
#+END_SRC

#+RESULTS:
: OBOL

** POS(N) = matches only if current position is N

#+BEGIN_SRC sh
snobol4 -b <<<'
 STR1 = "   SNOBOL4"
 STR2 = " SNOBOL4"

 STR1 SPAN(" ") POS(3) RTAB(0) . OUTPUT
 STR2 SPAN(" ") POS(3) RTAB(0) . OUTPUT
END
'
#+END_SRC

#+RESULTS:
: SNOBOL4

** RPOS(N) = matches only if current position is just left of the Nth character from the /end/

#+BEGIN_SRC sh
snobol4 -b <<<'
 STR = "SNOBOL4 iS    WEIRD"
 STR SPAN(" ") RPOS(5) RTAB(0) . OUTPUT
END
'
#+END_SRC

#+RESULTS:
: WEIRD

** FAIL = cause the scanner to seek alternatives even after finding a match

#+BEGIN_SRC sh :results scalar value
snobol4 -b <<<'
 "MISSISSIPPI" ("MI" | "IS" | "SI" | "IP" | "PI") $ OUTPUT FAIL
END
'
#+END_SRC

#+RESULTS:
: MI
: IS
: SI
: IS
: SI
: IP
: PI

** FENCE = stop scanner from trying a second pattern if the first fails

#+BEGIN_SRC sh :results scalar value
snobol4 -b <<<'
 OUTPUT = "BERATES => "
 "BERATES" (("BE" | "GE" | "FRE") FENCE ("TS" | "T")) . OUTPUT
 OUTPUT = "BETS => "
 "BETS" (("BE" | "GE" | "FRE") FENCE ("TS" | "T")) . OUTPUT
END
'
#+END_SRC

#+RESULTS:
: BERATES => 
: BETS => 
: BETS

** ABORT = immediate failure of entire pattern match

#+BEGIN_SRC sh
snobol4 -b <<<'
 SHORT = LEN(12) ABORT | RTAB(0) . LINE
 "abcdef" SHORT . OUTPUT
 "abcdefabcdefabcdef" SHORT . OUTPUT
END
'
#+END_SRC

#+RESULTS:
: abcdef

* Chapter 3: Primitive Functions
  :PROPERTIES:
  :header-args: :results scalar
  :END:

** INTEGER(N) checks if variable is convertable to an int

#+BEGIN_SRC sh
snobol4 <<<'
    INTEGER(4)      :F(ERR)
    INTEGER("4")    :F(ERR)
    INTEGER("X")    :F(ERR)
                    :S(END)
ERR OUTPUT = "Invalid input encountered."
END
'
#+END_SRC

#+RESULTS:
: Invalid input encountered.

** IDENT(X, Y) checks if X === Y (stricter than EQ(X, Y)

#+BEGIN_SRC sh
snobol4 <<<'
    X = 3
    Y = "3"
    IDENT(X,Y)    :S(YES)F(NO)
YES OUTPUT = X " === " Y :(END)
NO  OUTPUT = X " !== " Y :(END)
END
'
#+END_SRC

#+RESULTS:
: 3 !== 3

** DIFFER(X, Y) checks if X !== Y
** LGT(A, B) checks for alphabetic order

#+BEGIN_SRC sh :results scalar
snobol4 <<<'
        &ANCHOR = 1; &FULLSCAN = 1
        FLIP = LEN(*I) . HEAD LEN(1) $ X LEN(1) $ Y *LGT(X,Y)
        STR = "LABORATORIES"
        OUTPUT = STR
        LIMIT = SIZE(STR) - 2

LOOP    STR FLIP = HEAD Y X
#       OUTPUT = STR
        I = LT(I, LIMIT) I + 1    :S(LOOP)
        I = 0
        LIMIT = GT(LIMIT, 0) LIMIT - 1     :S(LOOP)
        OUTPUT = STR
END'
#+END_SRC

#+RESULTS:
: LABORATORIES
: AABEILOORRST

** SIZE(A) = length of a string
** REPLACE(str, [chars], [new chars])

#+BEGIN_SRC sh :results scalar
snobol4 <<< '
        BITS = OUTPUT = "1101111101"
        FLIPPED_BITS = OUTPUT = REPLACE(BITS, "01", "10")
END
'
#+END_SRC

#+RESULTS:
: 1101111101
: 0010000010

** TRIM(str) = removes leading and trailing spaces
** DUPL(str, n) = repeats str n times

#+BEGIN_SRC sh :results scalar
snobol4 <<<'
        TITLE = "Hello, World"

        BAR = "|"
        OSIZE = 50
        ISIZE = OSIZE - (SIZE(BAR) * 2)
        TSIZE = (ISIZE - SIZE(TITLE)) / 2
        LINE = DUPL("-", OSIZE)
        BARS = "|" DUPL(" ", ISIZE) "|"

        OUTPUT = LINE
        OUTPUT = BARS
        OUTPUT = BARS
        OUTPUT = BAR DUPL(" ", TSIZE) TITLE DUPL(" ", TSIZE) BAR
        OUTPUT = BARS
        OUTPUT = BARS
        OUTPUT = LINE
END
'
#+END_SRC

#+RESULTS:
: --------------------------------------------------
: |                                                |
: |                                                |
: |                  Hello, World                  |
: |                                                |
: |                                                |
: --------------------------------------------------

** REMDR = remainder

#+BEGIN_SRC sh
snobol4 <<< '
    OUTPUT = REMDR(13,2)
    OUTPUT = REMDR(43,8)
    OUTPUT = REMDR(-14, -5)
END
'
#+END_SRC

#+RESULTS:
|  1 |
|  3 |
| -4 |

** DATE() = current date

#+BEGIN_SRC sh :results scalar
snobol4 <<< '
        OUTPUT = DATE()
END
'
#+END_SRC

#+RESULTS:
: 08/02/2018 08:39:14

** TIME() = amount of time passed in ms since beginning of execution

#+BEGIN_SRC sh
snobol4 <<<'
    Y = OUTPUT = TIME()
    OUTPUT = DUPL("-", 30)
    OUTPUT = TIME() - Y
    OUTPUT = TIME() - Y
    OUTPUT = TIME() - Y
END
'
#+END_SRC

#+RESULTS:
|            0.00500000000000034 |
| ------------------------------ |
|             0.0229999999999997 |
|             0.0249999999999999 |
|             0.0269999999999997 |

** EVAL(str) = evaluates a string

#+BEGIN_SRC sh
snobol4 <<<'
        S = "(X * SIZE(X)) - 2"
        U = *((X * SIZE(X)) - 2)
        X = 5
        OUTPUT = EVAL(S)
        OUTPUT = EVAL(U)
END
'
#+END_SRC

#+RESULTS:
| 3 |
| 3 |

** APPLY(fn, arg1, ... , argN) = call function by name

#+BEGIN_SRC sh
snobol4 <<< '
        OUTPUT = APPLY("REMDR", 31, 12)
END
'
#+END_SRC

#+RESULTS:
: 7

** NEGATION = ~

The negation key is listed as ALT+170: ¬
But the actual negation key is a tilde: ~

#+BEGIN_SRC sh :results scalar
snobol4 <<<'
                X = 1
                Y = 2
                OUTPUT = DIFFER(X,Y) "They differ"                
                OUTPUT = DIFFER(X,X) "They do not differ"
                OUTPUT = ~(DIFFER(X,X)) "They do not differ"
END
'
#+END_SRC

#+RESULTS:
: They differ
: They do not differ

** INTERROGATION = ?

#+BEGIN_SRC sh :results scalar
snobol4 <<<'
                X = 1
                Y = 2
                OUTPUT = ?(DIFFER(X,Y)) "They differ"
                OUTPUT = ?(DIFFER(X,X)) "They do not differ"
END
'
#+END_SRC

#+RESULTS:
: They differ

** LOAD(prototype, fn) = add FORTRAN or ASSEMBLY libs
** OPSYN(x,y,n) = create aliases and override operators

#+BEGIN_SRC sh :results scalar
snobol4 <<<'
                OPSYN("ADD", "+", 2)

                OUTPUT = "FUN WITH " ADD(3,4)
                OUTPUT = DUPL("-", 20)

                DEFINE("PLUS(X,Y)")             :(END_PLUS)
PLUS            PLUS = X " + " Y " = " ADD(X,Y) :(RETURN)
END_PLUS

                OUTPUT = PLUS(3,4)
                OPSYN("+", "PLUS", 2)
                OUTPUT = 3 + 4
END
'
#+END_SRC

#+RESULTS:
: FUN WITH 7
: --------------------
: 3 + 4 = 7
: 3 + 4 = 7

* Chapter 4: Programmer Defined Functions
