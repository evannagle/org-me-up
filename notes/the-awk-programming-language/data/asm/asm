BEGIN {
      srcfile = ARGV[1]
      ARGV[1] = ""
      tempfile = "data/asm/asm.temp"

      n = split("const get put ld st add sub jpos jz j halt" , x)

      for(i = 1; i <= n; i++) {
      	op[x[i]] = i - 1
      }	     

      # ASSEMBLER PASS 1
      FS = "[ \t]+"
      while(getline <srcfile > 0) {
        sub(/#.*/,"") #remove comments
	symtab[$1] = nextmem
	if($2 != "") {
	  print $2 "\t" $3 > tempfile
	  nextmem++
	}
      }
      close(tempfile)
      
      # ASSEMBLER PASS 2
      nextmem = 0
      while(getline <tempfile > 0) {
        if($2 !~ /^[0-9]*$/) #if symbolic addr
	  $2 = symtab[$2]    #replace with numeric value
	mem[nextmem++] = 1000 * op[$1] + $2 # pack into word
      }

      #print "block", "opcode", "instruction", "address", "accumulator", "pc"

      for(pc = 0; pc >= 0 && valve++ < 100; ) {
        block = mem[pc++]
        addr = block % 1000
	code = int(block / 1000)

	if	(code == op["get"])  { getline acc || acc = 0 } #this is diff from the book
	else if (code == op["put"])  { print acc } 
	else if (code == op["st"])   { mem[addr] = acc }
	else if (code == op["ld"])   { acc = mem[addr]; }
	else if (code == op["add"])  { acc += mem[addr]; }
	else if (code == op["sub"])  { acc -= mem[addr]; }
	else if (code == op["jpos"]) { if (acc > 0) pc = addr }
	else if (code == op["jz"])   { if (acc == 0) pc = addr }
	else if (code == op["j"])    { pc = addr }
	else if (code == op["halt"]) { pc = -1 }
	else 	      	 	     { pc = -1 }
	
	#print block, code, x[code + 1], addr, acc, pc
      }
}
